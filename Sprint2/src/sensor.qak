System sprint2_sensors_model

Event doDeposit : doDeposit(X) 					 // user to cargoservice
Event waitingForDeposit : waitingfordeposit(X)    // sent after load request accepted, used to trigger the sonar sensor
Event stopWaitingForDeposit : stopWaitingForDeposit(X)

Event sonaralert : sonaralert(X)	 // inizia stato di alert	
Event sonarok    : sonarok(X)		 // terminato stato di alert

Dispatch measurement	: measurement(CM) // evento di invio della misurazione rilevata

Context ctx_cargo  ip  [host="192.168.202.158" port=8014]
Context ctx_sensor  ip  [host="localhost" port=8016]

/*
 * SONAR:
 * 	vcc: vcc 5v
 *  trig: 17
 * 	echo: 27
 * 	ground: gnd
 * 
 * LED:
 * 	gnd : gnd
 * 	vcc : 25
 * 
 */

QActor sonar context ctx_sensor{
	
	[#
		lateinit var reader : java.io.BufferedReader
	    lateinit var p : Process	
	    var D: Double = 0.0
	#]
	State s0 initial{
		delay 2000
		println("$name :: starting") color cyan
		[#
			p       = Runtime.getRuntime().exec("python sonar.py")
			reader  = java.io.BufferedReader( java.io.InputStreamReader(p.getInputStream()) )
		#]
	}
	Goto work
	
	State work{
		delay 1000
		[#
		
		var data = reader.readLine()
			
			if( data != null ){
				try{ 
			        val vd = data.toDouble()
			        D = vd
				}catch(e: Exception){
					CommUtils.outred("$name : ERROR FROM READING SONAR DATA: $e")
				}
			}
		
		#
		]
//		println("$name: distance - $D") color yellow
		forward sonarmanager -m measurement : measurement($D)	   
	}
	Goto work
	
}

QActor sonarmanager context ctx_sensor {
	
	[#
		val DFREE = 30
		var guasto = false
		var counterError = 0
		var counterDeposit = 0
		var doDepositSent = false
		var deposit = false
	#]
	
	State s0 initial{
		delay 3000
		println("$name : : starting") color cyan 

	} 
	Goto listen_for_measurement
	
	
	State listen_for_measurement {
		//aspetto
	}
	Transition t
		whenMsg measurement -> process
		whenEvent waitingForDeposit -> checkForDeposit
		whenEvent stopWaitingForDeposit -> stopWaitingDeposit
		
	State process{
		[# var M : Double = 0.0 #]
		onMsg(measurement : measurement(X)){
			[#
				M = payloadArg(0).toDouble()
			#]
//			println("$name :measure $M") color yellow
		}
		
		if[# M < DFREE/2 #]{
			println("$name : container presente $counterDeposit") color yellow
			[#
				counterError = 0
				counterDeposit += 1
			#]
			
			if[# deposit && counterDeposit >= 3 && !doDepositSent #]{
				println("$name : invio deposito") color green
				emit doDeposit : doDeposit(1)
				[# doDepositSent = true #]
			}
		}
		
		if[# M >= DFREE/2 && M <= DFREE #]{
			//println("$name : container assente") color cyan
			[#
				counterError = 0
				counterDeposit = 0
				doDepositSent = false
			#]
		}
		
		if[# M > DFREE #]{
			if [# !guasto #] { println("$name : possibile guasto") color red }
			[#
				counterError += 1
				counterDeposit = 0
				doDepositSent = false
			#]
		}
		
		if[# counterError >= 3 && !guasto #]{
			println("$name : GUASTO") color red
			[# 
				guasto = true 
			#]
			emit sonaralert : sonaralert(1)
		} else {
			if [# guasto && counterError < 3 #] {
				println("$name : GUASTO RIENTRATO") color yellow
				[# guasto = false #]
				emit sonarok : sonarok(1)
			}
		}
	}
	Goto listen_for_measurement
	
	State checkForDeposit{
		[#deposit = true #]
		println("$name : waiting for deposit") color cyan
		
		if[# counterDeposit >= 3 #]{
			println("$name : invio deposito") color green
			[# doDepositSent = true #]
			emit doDeposit : doDeposit(1)
			
		} 
		else {
			println("$name : deposit NOT confirmed: needing other measurements") color cyan
		}
	}
	Goto listen_for_measurement
	
	State stopWaitingDeposit{
		println("$name : stop waiting for deposit") color cyan
		[# deposit = false#]
	}
	Goto listen_for_measurement
}

QActor led context ctx_sensor{
	
	[# lateinit var p : Process #]
	
	State s0 initial{
		println("$name : starting") color cyan
 	}
 	Goto wait
 	
 	State wait{
 		println("$name : waiting for interrupts") color cyan
 	}
 	
 	Transition t
 		whenEvent sonaralert -> acceso
 		whenEvent sonarok -> spento
 		
 	State acceso{
 		println("$name : acceso") color red
 		[#
 			p       = Runtime.getRuntime().exec("python ledPython25On.py")	
 		#
 		]
 		
 	}
 	Goto wait
 	
 	State spento{
 		println("$name : spento") color green
 		[#
 			p       = Runtime.getRuntime().exec("python ledPython25Off.py")	
 		#
 		]
 	}
 	Goto wait
}

//QActor mockcore context ctx_sensor {
//	State s0 initial {
//		delay 20000
//		emit waitingForDeposit : waitingForDeposit(0)
//		
//		delay 10000
//		emit stopWaitingForDeposit : stopWaitingForDeposit(0)
//	}
//}